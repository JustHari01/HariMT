pluginManagement {
    repositories {
        gradlePluginPortal()
        mavenCentral()

        exclusiveContent {
            forRepository {
                maven {
                    name = 'Fabric'
                    url = uri('https://maven.fabricmc.net')
                }
            }
            filter {
                includeGroupByRegex("net\\.fabricmc(\\..+)?")  // Gets net.fabricmc and everything inside, including unpick
                includeGroup('fabric-loom')
            }
        }

        exclusiveContent {
            forRepository {
                maven {
                    name = 'Sponge'
                    url = 'https://repo.spongepowered.org/repository/maven-public'
                }
            }
            filter {
                includeGroup('org.spongepowered')
            }
        }

        maven {
            name = 'Forge'
            url = uri('https://maven.minecraftforge.net/')
        }

        maven {
            name = 'ParchmentMC'
            url = 'https://maven.parchmentmc.org'
        }
    }
}

plugins {
    id 'org.gradle.toolchains.foojay-resolver-convention' version '0.8.0'
}

// This should match the folder name of the project, or else IDEA may complain (see https://youtrack.jetbrains.com/issue/IDEA-317606)
rootProject.name = "HariMT"
include('common')
include('fabric')
include('forge')