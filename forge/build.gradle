plugins {
    id 'multiloader-loader'
    id 'net.minecraftforge.gradle'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
}

minecraft {
    mappings channel: 'parchment', version: "${parchment_version}-${parchment_minecraft}"

    def at = project(':common').file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformer = at.absolutePath
    }

    runs {
        configureEach {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', mod_id
            taskName = "Forge${name.capitalize()}${project.name.capitalize()}"
        }
        client {
            args '--username', 'Dev'
        }
        server {
        }
        data {
            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

repositories {
    maven { url = 'https://maven.bawnorton.com/releases' }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    implementation("io.github.llamalad7:mixinextras-common:${mixinextras_version}")
    annotationProcessor("io.github.llamalad7:mixinextras-common:${mixinextras_version}")
    compileOnly(annotationProcessor("com.github.bawnorton.mixinsquared:mixinsquared-common:${mixinsquared_version}"))
    implementation(jarJar("com.github.bawnorton.mixinsquared:mixinsquared-forge:[${mixinsquared_version},)"))
    compileOnly 'org.projectlombok:lombok:1.18.32'
    annotationProcessor 'org.projectlombok:lombok:1.18.32'
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

processResources {
    def expandProps = [
            "version": version,
            "group": project.group, // Else we target the task's group.
            "minecraft_version": minecraft_version,
            "forge_version": forge_version,
            "forge_loader_version_range": forge_loader_version_range,
            "mod_name": mod_name,
            "mod_author": mod_authors,
            "mod_id": mod_id,
            "license": license,
            "description": description,
            "mod_version": mod_version,
            "mod_authors": mod_authors,
            "minecraft_version_range": minecraft_version_range
    ]

    filesMatching('META-INF/forge.mods.toml') {
        expand expandProps
    }
    rename 'forge.mods.toml', 'mods.toml'
    inputs.properties expandProps
}

jar {
    exclude 'me/jellysquid/**'
}